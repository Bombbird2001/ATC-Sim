android {
    buildToolsVersion "28.0.3"
    compileSdkVersion 28
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            jniLibs.srcDirs = ['libs']
        }
        lite {
            assets.srcDirs = ['assetsLite']
            res.srcDirs = ['resLite']
        }
        full {
            assets.srcDirs = ['assetsFull']
            res.srcDirs = ['resFull']
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    packagingOptions {
        exclude 'META-INF/robovm/ios/robovm.xml'
    }
    defaultConfig {
        applicationId "com.bombbird.terminalcontrol"
        minSdkVersion 21
        targetSdkVersion 28
        ext {
            versionCode project.versionCode
            versionName version
        }
    }
    buildTypes {
        debug {
            
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        lite {
            dimension "version"
            applicationId "com.bombbird.terminalcontrollite"
            resValue "string", "app_name", "Terminal Control: Lite"
        }
        full {
            dimension "version"
            applicationId "com.bombbird.terminalcontrol"
            resValue "string", "app_name", "Terminal Control"
        }
    }

    lintOptions {
        abortOnError false
    }
}

//Called before build to check that version information in internal game files are correct for each version
task checkVersion() {
    String gradleVersion = android.defaultConfig.versionName
    String gradleVersionCode = android.defaultConfig.versionCode

    def text = project.file("../android/assetsFull/game/type.type").text
    def values = text.split(' ')
    if (values[0] != "full") {
        throw new GradleException("Incorrect type for full version: " + values[0])
    }
    if (values.size() < 3) {
        throw new GradleException("Full version: Type string length is " + values.size() + ", needs 3")
    }
    if ((values[1] != gradleVersion) || (values[2] != gradleVersionCode)) {
        throw new GradleException("Full version: " + values[0] + " " + values[1] + " code " + values[2] + " not equal to Gradle's " + gradleVersion + " code " + gradleVersionCode)
    }
    def text2 = project.file("../android/assetsLite/game/type.type").text
    def values2 = text2.split(' ')
    if (values2[0] != "lite") {
        throw new GradleException("Incorrect type for lite version: " + values2[0])
    }
    if (values2.size() < 3) {
        throw new GradleException("Lite version: Type string length is " + values2.size() + ", needs 3")
    }
    if ((values2[1] != gradleVersion) || (values2[2] != gradleVersionCode)) {
        throw new GradleException("Lite version: " + values2[0] + " " + values2[1] + " code " + values2[2] + " not equal to Gradle's " + gradleVersion + " code " + gradleVersionCode)
    }
}
assemble.dependsOn checkVersion

// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
task copyAndroidNatives() { 
    file("libs/armeabi/").mkdirs();
    file("libs/armeabi-v7a/").mkdirs();
    file("libs/arm64-v8a/").mkdirs();
    file("libs/x86_64/").mkdirs();
    file("libs/x86/").mkdirs();

    configurations.natives.files.each { jar ->
        def outputDir = null
        if(jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
        if(jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")        
        if(jar.name.endsWith("natives-armeabi.jar")) outputDir = file("libs/armeabi")
        if(jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
        if(jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
        if(outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.so"
            }
        }
    }
}

task run(type: Exec) {
    def path
    def localProperties = project.file("../local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        if (sdkDir) {
            path = sdkDir
        } else {
            path = "$System.env.ANDROID_HOME"
        }
    } else {
        path = "$System.env.ANDROID_HOME"
    }

    def adb = path + "/platform-tools/adb"
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.bombbird.terminalcontrol/com.bombbird.terminalcontrol.AndroidLauncher'
}

// sets up the Android Idea project, using the old Ant based build.
idea {
    module {
        sourceDirs += file("src");
        scopes = [ COMPILE: [plus:[project.configurations.compile]]]

        iml {
            withXml {
                def node = it.asNode()
                def builder = NodeBuilder.newInstance();
                builder.current = node;
                builder.component(name: "FacetManager") {
                    facet(type: "android", name: "Android") {
                        configuration {
                            option(name: "UPDATE_PROPERTY_FILES", value:"true")
                        }
                    }
                }
            }
        }
    }
}
